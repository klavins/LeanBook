<!DOCTYPE HTML>
<html lang="en" class="light sidebar-visible" dir="ltr">
    <head>
        <!-- Book generated using mdBook -->
        <meta charset="UTF-8">
        <title>Multiplication - Foundations of Lean</title>


        <!-- Custom HTML head -->

        <meta name="description" content="A light introduction to the foundations of mathematics and proof checking with Lean">
        <meta name="viewport" content="width=device-width, initial-scale=1">
        <meta name="theme-color" content="#ffffff">

        <link rel="icon" href="../favicon.svg">
        <link rel="shortcut icon" href="../favicon.png">
        <link rel="stylesheet" href="../css/variables.css">
        <link rel="stylesheet" href="../css/general.css">
        <link rel="stylesheet" href="../css/chrome.css">
        <link rel="stylesheet" href="../css/print.css" media="print">

        <!-- Fonts -->
        <link rel="stylesheet" href="../FontAwesome/css/font-awesome.css">
        <link rel="stylesheet" href="../fonts/fonts.css">

        <!-- Highlight.js Stylesheets -->
        <link rel="stylesheet" id="highlight-css" href="../highlight.css">
        <link rel="stylesheet" id="tomorrow-night-css" href="../tomorrow-night.css">
        <link rel="stylesheet" id="ayu-highlight-css" href="../ayu-highlight.css">

        <!-- Custom theme stylesheets -->
        <link rel="stylesheet" href="../lean-book.css">

        <!-- MathJax -->
        <script async src="https://cdnjs.cloudflare.com/ajax/libs/mathjax/2.7.1/MathJax.js?config=TeX-AMS-MML_HTMLorMML"></script>

        <!-- Provide site root and default themes to javascript -->
        <script>
            const path_to_root = "../";
            const default_light_theme = "light";
            const default_dark_theme = "navy";
        </script>
        <!-- Start loading toc.js asap -->
        <script src="../toc.js"></script>
    </head>
    <body>
    <div id="body-container">
        <!-- Work around some values being stored in localStorage wrapped in quotes -->
        <script>
            try {
                let theme = localStorage.getItem('mdbook-theme');
                let sidebar = localStorage.getItem('mdbook-sidebar');

                if (theme.startsWith('"') && theme.endsWith('"')) {
                    localStorage.setItem('mdbook-theme', theme.slice(1, theme.length - 1));
                }

                if (sidebar.startsWith('"') && sidebar.endsWith('"')) {
                    localStorage.setItem('mdbook-sidebar', sidebar.slice(1, sidebar.length - 1));
                }
            } catch (e) { }
        </script>

        <!-- Set the theme before any content is loaded, prevents flash -->
        <script>
            const default_theme = window.matchMedia("(prefers-color-scheme: dark)").matches ? default_dark_theme : default_light_theme;
            let theme;
            try { theme = localStorage.getItem('mdbook-theme'); } catch(e) { }
            if (theme === null || theme === undefined) { theme = default_theme; }
            const html = document.documentElement;
            html.classList.remove('light')
            html.classList.add(theme);
            html.classList.add("js");
        </script>

        <input type="checkbox" id="sidebar-toggle-anchor" class="hidden">

        <!-- Hide / unhide sidebar before it is displayed -->
        <script>
            let sidebar = null;
            const sidebar_toggle = document.getElementById("sidebar-toggle-anchor");
            if (document.body.clientWidth >= 1080) {
                try { sidebar = localStorage.getItem('mdbook-sidebar'); } catch(e) { }
                sidebar = sidebar || 'visible';
            } else {
                sidebar = 'hidden';
            }
            sidebar_toggle.checked = sidebar === 'visible';
            html.classList.remove('sidebar-visible');
            html.classList.add("sidebar-" + sidebar);
        </script>

        <nav id="sidebar" class="sidebar" aria-label="Table of contents">
            <!-- populated by js -->
            <mdbook-sidebar-scrollbox class="sidebar-scrollbox"></mdbook-sidebar-scrollbox>
            <noscript>
                <iframe class="sidebar-iframe-outer" src="../toc.html"></iframe>
            </noscript>
            <div id="sidebar-resize-handle" class="sidebar-resize-handle">
                <div class="sidebar-resize-indicator"></div>
            </div>
        </nav>

        <div id="page-wrapper" class="page-wrapper">

            <div class="page">
                <div id="menu-bar-hover-placeholder"></div>
                <div id="menu-bar" class="menu-bar sticky">
                    <div class="left-buttons">
                        <label id="sidebar-toggle" class="icon-button" for="sidebar-toggle-anchor" title="Toggle Table of Contents" aria-label="Toggle Table of Contents" aria-controls="sidebar">
                            <i class="fa fa-bars"></i>
                        </label>
                        <button id="theme-toggle" class="icon-button" type="button" title="Change theme" aria-label="Change theme" aria-haspopup="true" aria-expanded="false" aria-controls="theme-list">
                            <i class="fa fa-paint-brush"></i>
                        </button>
                        <ul id="theme-list" class="theme-popup" aria-label="Themes" role="menu">
                            <li role="none"><button role="menuitem" class="theme" id="default_theme">Auto</button></li>
                            <li role="none"><button role="menuitem" class="theme" id="light">Light</button></li>
                            <li role="none"><button role="menuitem" class="theme" id="rust">Rust</button></li>
                            <li role="none"><button role="menuitem" class="theme" id="coal">Coal</button></li>
                            <li role="none"><button role="menuitem" class="theme" id="navy">Navy</button></li>
                            <li role="none"><button role="menuitem" class="theme" id="ayu">Ayu</button></li>
                        </ul>
                        <button id="search-toggle" class="icon-button" type="button" title="Search. (Shortkey: s)" aria-label="Toggle Searchbar" aria-expanded="false" aria-keyshortcuts="S" aria-controls="searchbar">
                            <i class="fa fa-search"></i>
                        </button>
                    </div>

                    <h1 class="menu-title">Foundations of Lean</h1>

                    <div class="right-buttons">
                        <a href="../print.html" title="Print this book" aria-label="Print this book">
                            <i id="print-button" class="fa fa-print"></i>
                        </a>
                        <a href="https://github.com/klavins/LeanBook" title="Git repository" aria-label="Git repository">
                            <i id="git-repository-button" class="fa fa-github"></i>
                        </a>

                    </div>
                </div>

                <div id="search-wrapper" class="hidden">
                    <form id="searchbar-outer" class="searchbar-outer">
                        <input type="search" id="searchbar" name="searchbar" placeholder="Search this book ..." aria-controls="searchresults-outer" aria-describedby="searchresults-header">
                    </form>
                    <div id="searchresults-outer" class="searchresults-outer hidden">
                        <div id="searchresults-header" class="searchresults-header"></div>
                        <ul id="searchresults">
                        </ul>
                    </div>
                </div>

                <!-- Apply ARIA attributes after the sidebar and the sidebar toggle button are added to the DOM -->
                <script>
                    document.getElementById('sidebar-toggle').setAttribute('aria-expanded', sidebar === 'visible');
                    document.getElementById('sidebar').setAttribute('aria-hidden', sidebar !== 'visible');
                    Array.from(document.querySelectorAll('#sidebar a')).forEach(function(link) {
                        link.setAttribute('tabIndex', sidebar === 'visible' ? 0 : -1);
                    });
                </script>

                <div id="content" class="content">
                    <main>
                        <div style='display:none'>
--  Copyright (C) 2025  Eric Klavins
--
--  This program is free software: you can redistribute it and/or modify
--  it under the terms of the GNU General Public License as published by
--  the Free Software Foundation, either version 3 of the License, or
--  (at your option) any later version.   
</div>
<p><span style='color: orange'><em><strong>UNDER CONSTRUCTION</strong></em></span><br>
<span style='color: lightgray; font-size: 10pt'><a href='https://github.com/klavins/LeanBook/blob/main/main/../LeanBook/Chapters/Reals/Multiply.lean'>Code</a> for this chapter</span></p>
<h1 id="multiplication"><a class="header" href="#multiplication">Multiplication</a></h1>
<p>Multiplication of Dedekind cuts is straightfoward, but also fairly involved, or as Rudin says: "bothersome". The issue is dealing with both positive and negative cuts. Following Rudin, the development of the definitions proceeds as follows:</p>
<ul>
<li>Define multiplication on positive cuts.</li>
<li>Extend multiplciation on positive cuts to non-negative cuts, building on the previous step by handling the cases in which either or both of the cuts is zero.</li>
<li>Extend multiplication on non-negative cuts to all cuts.</li>
</ul>
<p>For each of the above definitions of multiplication, we also prove the properties required to show that multiplication forms a commutiative group on cuts:</p>
<ul>
<li>0 is an identity: <code>0*x = 0</code></li>
<li>Multiplication is commutative: <code>x*y = y*x</code></li>
<li>Associativity: <code>x*(y*z) = (x*y)*z</code>
The last property is particularly challenging, because to relate arbitary reals with positive reals, one has to deal with an abundance of cases, preferably gracefully. Thus, along the way we will prove several intermediate results which allow us to make the proof more concise.</li>
</ul>
<h2 id="definitions"><a class="header" href="#definitions">Definitions</a></h2>
<h3 id="multiplication-of-positive-cuts"><a class="header" href="#multiplication-of-positive-cuts">Multiplication of Positive Cuts</a></h3>
<p>Given two positive cuts <code>0 &lt; a</code> and <code>0 &lt; b</code>, their product is the set of points <code>z</code> such that for some <code>x ∈ a</code> and <code>y ∈ b</code>, <code>z &lt; x*y</code>. This basic definition underlies the entire framework for multiplication, after which we simply have to deal with various combinations of non-negative and negative numbers.</p>
<pre><code class="language-lean">def pre_mul (a b : DCut) :=
  { z | ∃ x ∈ a.A, ∃ y ∈ b.A, 0 &lt; x ∧ 0 &lt; y ∧ z &lt; x*y }

theorem pre_mulalt (a b : DCut)
  :  { z | ∃ x ∈ a.A, ∃ y ∈ b.A, 0 &lt; x ∧ 0 &lt; y ∧ z &lt; x*y } =
     { z | ∃ x y, x ∈ a.A ∧ y ∈ b.A ∧ 0 &lt; x ∧ 0 &lt; y ∧ z &lt; x*y } := by
  simp
</code></pre>
<p>To prove that this definition results in a cut, we need to prove as usual that it is non-empty, not equal to ℚ, downward closed, and open.</p>
<pre><code class="language-lean">theorem pre_mul_ne {a b : DCut} (ha : 0 &lt; a) (hb : 0 &lt; b) : ∃ q, q ∈ pre_mul a b := by

  have ⟨ x, ⟨ hx1, hx2 ⟩ ⟩ := a.op 0 (zero_in_pos ha)
  have ⟨ y, ⟨ hy1, hy2 ⟩ ⟩ := b.op 0 (zero_in_pos hb)
  have hxy : 0 &lt; x * y := Left.mul_pos hx2 hy2
  use -1, x, hx1, y, hy1, hx2, hy2
  exact gt_of_gt_of_ge hxy rfl

theorem pre_mul_nf {a b : DCut} (ha : 0 &lt; a) (_ : 0 &lt; b)
  : ∃ q, q ∉ pre_mul a b := by

  obtain ⟨ x, hx ⟩ := a.nf
  obtain ⟨ y, hy ⟩ := b.nf
  use x*y

  have hxpos : 0 &lt; x := a_lt_b (zero_in_pos ha) hx
  have hx' : ∀ q ∈ a.A, q &lt; x := by intro q hq; exact a_lt_b hq hx
  have hy' : ∀ q ∈ b.A, q &lt; y := by intro q hq; exact a_lt_b hq hy

  simp[pre_mul]
  intro s hs t ht hsp htp
  apply @_root_.le_of_lt
  exact mul_lt_mul_of_pos' (hx' s hs) (hy' t ht) htp hxpos

theorem pre_mul_dc {a b : DCut} : ∀ x y, x ≤ y ∧ y ∈ (pre_mul a b) → x ∈ (pre_mul a b) := by
  intro x y ⟨ hxy, hy ⟩
  obtain ⟨ s, ⟨ hs, ⟨ t, ⟨ ht, ⟨ hsp, ⟨ htp, hyst ⟩ ⟩ ⟩ ⟩ ⟩ ⟩ := hy
  exact ⟨ s, ⟨ hs, ⟨ t, ⟨ ht, ⟨ hsp, ⟨ htp, lt_of_le_of_lt hxy hyst ⟩ ⟩ ⟩ ⟩ ⟩ ⟩

theorem pre_mul_op {a b : DCut} : ∀ x ∈ (pre_mul a b), ∃ y ∈ (pre_mul a b), x &lt; y := by
  intro x ⟨ s, ⟨ hs, ⟨ t, ⟨ ht, ⟨ hsp, ⟨ htp, hyst ⟩ ⟩ ⟩ ⟩ ⟩ ⟩
  have ⟨ s', ⟨ hs', hss' ⟩ ⟩ := a.op s hs
  have ⟨ t', ⟨ ht', htt' ⟩ ⟩ := b.op t ht
  have h: s*t &lt; s'*t' := mul_lt_mul_of_pos' hss' htt' htp (by linarith)
  use s*t
  apply And.intro
  . exact ⟨ s', ⟨ hs', ⟨ t', ⟨ ht', ⟨ by linarith, ⟨ by linarith, h ⟩ ⟩ ⟩ ⟩ ⟩ ⟩
  . linarith
</code></pre>
<p>Grouping these properties together we get:</p>
<pre><code class="language-lean">def mul_pos (a b : DCut) (ha : 0 &lt; a) (hb : 0 &lt; b) : DCut :=
  ⟨ pre_mul a b, pre_mul_ne ha hb, pre_mul_nf ha hb, pre_mul_dc, pre_mul_op ⟩
</code></pre>
<p>We will need the following property to extend multiplication from positive numbers to non-negative numbers stating that the product of two positive numbers is again positive. Thus, the definition <code>pre_mul</code> operates exclusively on the positive reals.</p>
<pre><code class="language-lean">theorem mul_is_pos {a b : DCut} (ha : 0 &lt; a) (hb : 0 &lt; b) : 0 &lt; mul_pos a b ha hb := by
  simp[lt_inst,mul_pos,DCut.ext_iff]
  have ⟨ x, ⟨ hx1, hx2 ⟩ ⟩ := non_neg_in_pos ha
  have ⟨ y, ⟨ hy1, hy2 ⟩ ⟩ := non_neg_in_pos hb
  apply And.intro
  . intro h
    simp[Set.Subset.antisymm_iff] at h
    have ⟨ h1, h2 ⟩ := h
    simp[pre_mul,zero_rw,odown] at h2
    have := h2 0 x hx1 y hy1 hx2 hy2 (Left.mul_pos hx2 hy2)
    simp_all
  . simp[pre_mul,zero_rw,odown]
    intro q hq
    have : q &lt; x*y := gt_trans (Left.mul_pos hx2 hy2) hq
    exact ⟨ x, ⟨ hx1, ⟨ y, ⟨ hy1, ⟨ hx2, ⟨ hy2, this ⟩ ⟩ ⟩ ⟩ ⟩ ⟩
</code></pre>
<h3 id="multiplication-on-non-negative-cuts"><a class="header" href="#multiplication-on-non-negative-cuts">Multiplication on Non-negative Cuts</a></h3>
<p>We now extend the definition to non-negative reals, essentially dealing with the cases in which either cut is zero, in which case the resulting product is zero. Indeed, if one of <code>a</code> and <code>b</code> is zero, then <code>pre_mul a b = ∅</code>.</p>
<pre><code class="language-lean">@[simp]
theorem zero_mul_empty {a b : DCut} (h : 0 = a ∨ 0 = b) : pre_mul a b = ∅ := by
  apply Or.elim h
  repeat
  . intro h'
    simp[DCut.ext_iff,zero_rw] at h'
    simp[pre_mul,←h',odown]
    ext q
    simp
    intro x hx y hy h1 h2
    linarith
</code></pre>
<p>Since <code>∅</code> is not a valid cut, we use <code>pre_mul a b ∪ odown 0</code> to represent the product of two non-negative cuts. The remaining theorems are required to show that the result is a cut. We simply, and laboriously, have to deal with the possible cases.</p>
<pre><code class="language-lean">theorem mul_nneg_ne {a b : DCut}
  : ∃ q, q ∈ pre_mul a b ∪ odown 0 := by
  use -1
  apply Or.inr
  simp[odown]

theorem mul_nneg_nf {a b : DCut} (ha : 0 ≤ a) (hb : 0 ≤ b)
  : ∃ q, q ∉ pre_mul a b ∪ odown 0 := by
  by_cases h0 : 0 &lt; a ∧ 0 &lt; b
  . have ⟨ q, hq ⟩ := pre_mul_nf h0.left h0.right
    use q
    intro h1
    simp_all
    exact hq ((mul_is_pos h0.left h0.right).right h1)
  . have hab : 0 = a ∨ 0 = b := by
      simp_all[lt_of_le]
      exact Or.symm (or_iff_not_imp_right.mpr h0)
    simp[hab,odown]
    exact ⟨ 1, rfl ⟩

theorem mul_nneg_dc {a b : DCut} {x y : ℚ}
  : x ≤ y ∧ y ∈ pre_mul a b ∪ odown 0 → x ∈ pre_mul a b ∪ odown 0 := by
  intro ⟨ h1, h2 ⟩
  apply Or.elim h2
  . intro hy
    exact Or.inl (pre_mul_dc x y ⟨ h1, hy ⟩)
  . intro hy
    apply Or.inr
    simp_all[odown]
    linarith

theorem mul_nneg_op {a b : DCut} (x : ℚ) :
  x ∈ pre_mul a b ∪ odown 0 → ∃ y ∈ pre_mul a b ∪ odown 0, x &lt; y := by
  intro h
  apply Or.elim h
  . intro hx
    have ⟨ q, ⟨ hq1, hq2 ⟩ ⟩ := pre_mul_op x hx
    exact ⟨ q, ⟨ Or.inl hq1, hq2 ⟩  ⟩
  . intro hx
    simp[odown] at hx ⊢
    exact ⟨ x/2, ⟨ by apply Or.inr; linarith, by linarith ⟩ ⟩

def mul_nneg (a b : DCut) (ha : 0 ≤ a) (hb : 0 ≤ b) : DCut :=
  ⟨ pre_mul a b ∪ odown 0,
    mul_nneg_ne,
    mul_nneg_nf ha hb,
    @mul_nneg_dc a b,
    @mul_nneg_op a b ⟩
</code></pre>
<h3 id="mulitiplication-on-arbitrary-cuts"><a class="header" href="#mulitiplication-on-arbitrary-cuts">Mulitiplication on Arbitrary Cuts</a></h3>
<p>Finally, we extend multiiplcation to arbitrary cuts. This step uses the fact that every cut <code>a</code> can be written as the difference <code>max 0 a - max 0 (-a)</code>. If <code>0 ≤ a</code> then</p>
<pre><code class="language-hs">max 0 a - max 0 (-a) = a + 0
</code></pre>
<p>and if <code>a &lt; 0</code> then</p>
<pre><code class="language-hs">max 0 a - max 0 (-a) = 0 + -a
</code></pre>
<p>both of which are non-negative, and we can therefore use the previous definition of multiplication on non-negative cuts.</p>
<pre><code class="language-lean">def mul (a b : DCut) : DCut :=
  let ap := maximum 0 a
  let an := maximum 0 (-a)
  let bp := maximum 0 b
  let bn := maximum 0 (-b)
  (mul_nneg ap bp (max_gz _) (max_gz _)) +
  (mul_nneg an bn (max_gz _) (max_gz _)) -
  (mul_nneg ap bn (max_gz _) (max_gz _)) -
  (mul_nneg an bp (max_gz _) (max_gz _))
</code></pre>
<p>We may now instantiate the notation classes for multiplcation.</p>
<pre><code class="language-lean">instance hmul_inst : HMul DCut DCut DCut := ⟨ mul ⟩
instance mul_inst : Mul DCut := ⟨ mul ⟩

example : (1:DCut) * 0 = 0 := by
  simp[hmul_inst,mul]
</code></pre>
<h2 id="multiplication-by-0"><a class="header" href="#multiplication-by-0">Multiplication by 0</a></h2>
<p>For non-negative cuts, it is useful to know that <code>0*a = 0</code> and <code>a*0 = 0</code>, as these properties allow us to reason about the zero cases in the non-negative commutativity proof. These properties also allow us to show that <code>0</code> is the multiplicative identity, which is needed for proving cuts with multiplication form a group.</p>
<pre><code class="language-lean">@[simp]
theorem mul_nneg_zero_left {a : DCut} (ha: 0 ≤ a)
  : mul_nneg 0 a (λ _ a =&gt; a) ha = 0 := by
  simp[mul_nneg,DCut.ext_iff,pre_mul,zero_rw]
  intro q hq
  simp_all[odown]
  obtain ⟨ x, ⟨ hx, ⟨ y, ⟨ hy, h ⟩ ⟩ ⟩ ⟩ := hq
  linarith

@[simp]
theorem mul_nneg_zero_right {a : DCut} (ha: 0 ≤ a)
  : mul_nneg a 0 ha (λ _ a =&gt; a) = 0 := by
  simp[mul_nneg,DCut.ext_iff,pre_mul,zero_rw]
  intro q hq
  simp_all[odown]
  obtain ⟨ x, ⟨ hx, ⟨ y, ⟨ hy, h ⟩ ⟩ ⟩ ⟩ := hq
  linarith
</code></pre>
<p>These two theorems allow us to prove that the multiple of two non-negative cuts is again non-negative.</p>
<pre><code class="language-lean">@[simp]
theorem mul_is_nneg {a b : DCut} (ha : 0 ≤ a) (hb : 0 ≤ b)
  : 0 ≤ mul_nneg a b ha hb := by
  by_cases h : 0 &lt; a ∧ 0 &lt; b
  . have := mul_is_pos h.left h.right
    simp[lt_inst,mul_pos] at this
    have ⟨ h1, h2 ⟩ := this
    simp[le_inst,mul_nneg]
    exact Set.subset_union_right
  . by_cases hb0 : 0 = a
    . simp[←hb0]
    . simp_all[lt_of_le]
      simp[←h]
</code></pre>
<p>We can extend these properties to arbitrary reals.</p>
<pre><code class="language-lean">@[simp]
theorem mul_zero_left {a : DCut} : 0 * a = 0 := by
  simp[hmul_inst,mul]

@[simp]
theorem mul_zero_right {a : DCut} : a * 0 = 0 := by
  simp[hmul_inst,mul]
</code></pre>
<h2 id="multiplication-by-1"><a class="header" href="#multiplication-by-1">Multiplication by 1</a></h2>
<pre><code class="language-lean">@[simp]
theorem mul_pos_id_left {a : DCut} (ha: 0 &lt; a)
  : mul_pos 1 a zero_lt_one ha = a := by
  simp[DCut.ext_iff,mul_pos,pre_mul,one_rw,odown]
  ext q
  constructor
  . intro ⟨ x, ⟨ hx, ⟨ y, ⟨ hy, ⟨ hx0, ⟨ hy0, hqxy ⟩ ⟩ ⟩ ⟩ ⟩ ⟩
    have hxy : x*y &lt; y := mul_lt_of_lt_one_left hy0 hx
    have hxy' := a.dc (x*y) y ⟨ by linarith, hy ⟩
    exact a.dc q (x*y) ⟨ by linarith, hxy' ⟩
  . intro hq
    by_cases h : 0 &lt; q
    . have ⟨s, ⟨ hs1, hs2 ⟩ ⟩ := a.op q hq
      have ⟨t, ⟨ ht1, ht2 ⟩ ⟩ := a.op s hs1
      use q/s
      have hs3 : 0 &lt; s := by linarith
      apply And.intro
      . exact Bound.div_lt_one_of_pos_of_lt hs3 hs2
      . use t
        have hts : t/s &gt; 1 := (one_lt_div hs3).mpr ht2
        have h1 : q*(t/s) &gt; q := (lt_mul_iff_one_lt_right h).mpr hts
        have h2 : q*(t/s) = q / s * t := Eq.symm (mul_comm_div q s t)
        exact ⟨ ht1, ⟨ div_pos h hs3, ⟨ by linarith, by linarith ⟩ ⟩ ⟩
    . have ⟨s, ⟨ hs1, hs2 ⟩ ⟩ := a.op 0 (zero_in_pos ha)
      use 1/2
      apply And.intro
      . linarith
      . exact ⟨ s, ⟨ hs1, ⟨ by linarith, ⟨ hs2, by linarith ⟩ ⟩ ⟩ ⟩

@[simp]
theorem mul_nneg_id_left {a : DCut} (ha: 0 ≤ a)
  : mul_nneg 1 a zero_le_one ha = a := by
    rw[le_of_lt] at ha
    apply Or.elim ha
    intro ha0
    . simp[←ha0]
    . intro ha'
      have := mul_pos_id_left ha'
      simp_all[mul_pos,DCut.ext_iff,mul_nneg,DCut.ext_iff]
      exact ha
</code></pre>
<h2 id="commutativity"><a class="header" href="#commutativity">Commutativity</a></h2>
<p>For positive cuts, commutativity is straightfoward, as it simply amounts to reordering x and y in the definition of <code>pre_mul</code>.</p>
<pre><code class="language-lean">theorem mul_pos_comm {a b : DCut} (ha : 0 &lt; a) (hb : 0 &lt; b)
  : mul_pos a b ha hb = mul_pos b a hb ha  := by
  ext q
  constructor
  repeat
  . intro ⟨ x, ⟨ hx, ⟨ y, ⟨ hy, ⟨ h1, ⟨ h2, h3 ⟩ ⟩ ⟩ ⟩ ⟩ ⟩
    exact ⟨ y, ⟨ hy, ⟨ x, ⟨ hx, ⟨ h2, ⟨ h1, by linarith ⟩ ⟩ ⟩ ⟩ ⟩ ⟩
</code></pre>
<p>Proving commutativity for non-negative cuts amounts to three cases, where <code>a</code> and <code>b</code> are both positive and where one or the other is negative.</p>
<pre><code class="language-lean">theorem mul_nneg_comm {a b : DCut} (ha : 0 ≤ a) (hb : 0 ≤ b)
  : mul_nneg a b ha hb = mul_nneg b a hb ha := by
  by_cases h : 0 &lt; a ∧ 0 &lt; b
  . simp[mul_nneg]
    have := mul_pos_comm h.left h.right
    simp_all[mul_pos]
  . simp[lt_of_le] at h
    by_cases hb0 : 0 = b
    . simp[←hb0]
    . simp_all
      simp[←h]
</code></pre>
<p>The proof of commutativity for arbitrary cuts requires us to reason about every possible combination of non-negative and negative cuts. We do this with the theorem <code>two_ineqs_true</code> which enuerates all four cases. For each case, the same (somewhat massive) simplificiation works.</p>
<pre><code class="language-lean">theorem mul_comm {a b : DCut}: a*b = b*a := by
  rcases two_ineqs a b with ⟨ ha, hb ⟩ | ⟨ ha, hb ⟩ | ⟨ ha, hb ⟩ | ⟨ ha, hb ⟩
  repeat
  simp[ha,hb,hmul_inst,mul,mul_nneg_comm,neg_le.mp]
</code></pre>
<p>Commutativity makes it easy to prove similar versions of theorems for which the one side has already been established. For example:</p>
<pre><code class="language-lean">@[simp]
theorem mul_nneg_id_right {a : DCut} (ha: 0 ≤ a)
  : mul_nneg a 1 ha zero_le_one = a := by
  rw[mul_nneg_comm,mul_nneg_id_left]
</code></pre>
<p>And similarly,</p>
<pre><code class="language-lean">@[simp]
theorem mul_id_right {a : DCut} : a * 1 = a := by
  simp only[hmul_inst,mul]
  by_cases ha : 0 &lt; a
  . simp[ha]
  . simp
    rw[not_gt_to_le] at ha
    simp[ha]

@[simp]
theorem mul_id_left {a : DCut} : 1 * a = a := by
  simp[mul_comm]
</code></pre>
<h2 id="associativity"><a class="header" href="#associativity">Associativity</a></h2>
<p>The proof that <code>mul_pos</code> is associatve amounts to a lot of book-keeping around some simple observations.</p>
<pre><code class="language-lean">theorem mul_pos_assoc {a b c : DCut} (ha : 0 &lt; a) (hb : 0 &lt; b) (hc : 0 &lt; c)
  : mul_pos a (mul_pos b c hb hc) ha (mul_is_pos hb hc) =
    mul_pos (mul_pos a b ha hb) c (mul_is_pos ha hb) hc  := by

  ext q
  constructor

  . choose x hx yz h' hx0 hyz0 hq
    choose y hy z hz hy0 hz0 hyz' using h'
    obtain ⟨ x', ⟨ hx', hxx' ⟩ ⟩ : ∃ x' ∈ a.A, x &lt; x' := a.op x hx
    use x*y
    constructor
    . use! x', hx', y
      simp_all
      nlinarith
    . use z
      simp_all
      nlinarith

  . choose xy h' z hz hxy hx0 hq
    choose x hx y hy hz0 hy0 hxy' using h'
    have ⟨ z', ⟨ hz', hzz' ⟩ ⟩ : ∃ z' ∈ c.A, z &lt;z' := c.op z hz
    use! x, hx, y*z
    constructor
    . use y, hy, z'
      simp_all
      nlinarith
    . simp_all
      nlinarith


@[simp]
theorem pre_mul_with_pos {a b : DCut} (ha : 0 &lt; a) (hb : 0 &lt; b)
  : pre_mul a b ∪ odown 0 = pre_mul a b := by
    have := mul_is_pos ha hb
    simp_all[mul_pos,lt_inst,zero_rw]

@[simp]
theorem mul_nneg_assoc {a b c : DCut} (ha : 0 ≤ a) (hb : 0 ≤ b) (hc : 0 ≤ c)
  : mul_nneg a (mul_nneg b c hb hc) ha (mul_is_nneg hb hc) =
    mul_nneg (mul_nneg a b ha hb) c (mul_is_nneg ha hb) hc := by

  rcases three_nn_ineqs ha hb hc with ⟨ ha', hb', hc' ⟩ | h | h | h
  . simp[mul_nneg]
    congr -- removes `∪ odown 0`
    simpa[mul_pos,ha',hb',hc'] using mul_pos_assoc ha' hb' hc'

  repeat
  . simp[h]
</code></pre>
<p>When a ≤ 0 while 0 ≤ b and 0 ≤ c then</p>
<pre><code class="language-hs">(a*b)*c = a*(b*c)
</code></pre>
<p>becomes</p>
<pre><code class="language-hs">-((-a)*b)*c = -((-a)*(b*c))
</code></pre>
<p>and then use mul_assoc_all_nn. Similarly, we can do all the other cases this way.</p>
<pre><code class="language-lean">@[simp]
theorem mul_neg_dist_left {a b : DCut} : a*(-b) = -(a*b) := by
  simp[hmul_inst,mul]
  rcases two_ineqs a b with ⟨ ha, hb ⟩ | ⟨ ha, hb ⟩ | ⟨ ha, hb ⟩ | ⟨ ha, hb ⟩
  repeat
  simp[ha,hb,neg_le.mp]

@[simp]
theorem mul_neg_dist_right {a b : DCut} : (-a)*b = -(a*b) := by
  simp only[hmul_inst,mul]
  rcases two_ineqs a b with ⟨ ha, hb ⟩ | ⟨ ha, hb ⟩ | ⟨ ha, hb ⟩ | ⟨ ha, hb ⟩
  repeat
  simp[ha,hb,neg_le.mp]

theorem mul_assoc_all_nn {a b c : DCut} (ha : 0 ≤ a) (hb : 0 ≤ b) (hc : 0 ≤ c)
  : a * (b * c) = (a * b) * c := by
  simp[hmul_inst,mul]
  simp[ha,hb,hc,neg_le.mp] -- uses mul_nneg_assoc

theorem flip {a : DCut} (ha: a &lt; 0) : 0 ≤ -a := neg_le'.mp (lt_imp_le ha)

theorem mul_assoc {a b c : DCut} : a * (b * c) = (a * b) * c := by
  rcases three_ineqs a b c with ⟨ ha, hb, hc ⟩ | ⟨ ha, hb, hc ⟩ |
                                ⟨ ha, hb, hc ⟩ | ⟨ ha, hb, hc ⟩ |
                                ⟨ ha, hb, hc ⟩ | ⟨ ha, hb, hc ⟩ |
                                ⟨ ha, hb, hc ⟩ | ⟨ ha, hb, hc ⟩
  . exact mul_assoc_all_nn ha hb hc
  . simpa using mul_assoc_all_nn (flip ha) hb hc
  . simpa using mul_assoc_all_nn ha (flip hb) hc
  . simpa using mul_assoc_all_nn ha hb (flip hc)
  . simpa using mul_assoc_all_nn (flip ha) (flip hb) hc
  . simpa using mul_assoc_all_nn (flip ha) hb (flip hc)
  . simpa using mul_assoc_all_nn ha (flip hb) (flip hc)
  . simpa using mul_assoc_all_nn (flip ha) (flip hb) (flip hc)
</code></pre>
<h2 id="instantiating-multiplication-classes"><a class="header" href="#instantiating-multiplication-classes">Instantiating Multiplication Classes</a></h2>
<pre><code class="language-lean">instance mul_zero_inst : MulZeroClass DCut := ⟨
    @mul_zero_left,
    @mul_zero_right
  ⟩

instance mul_one_inst : MulOneClass DCut := ⟨
  @mul_id_left,
  @mul_id_right
⟩

instance semigroup_inst : Semigroup DCut := ⟨
  λ x y z =&gt; Eq.symm (@mul_assoc x y z)
⟩

instance semigroup_w_zero_inst : SemigroupWithZero DCut := ⟨ -- is this auotgenerated?
  @mul_zero_left,
  @mul_zero_right
⟩

instance mul_zo_inst : MulZeroOneClass DCut := ⟨  -- is this auotgenerated?
  @mul_zero_left,
  @mul_zero_right
⟩

instance comm_magma_inst : CommMagma DCut := ⟨ @mul_comm ⟩

instance comm_semigroup_inst : CommSemigroup DCut := ⟨ @mul_comm ⟩

example {x y z :DCut} : (x+y)*z = z*(y+x) := by
  rw[@_root_.mul_comm] -- This is mathlib's CommMagma mul_com
  abel_nf
</code></pre>
<h2 id="natural-powers-and-monoid-instance"><a class="header" href="#natural-powers-and-monoid-instance">Natural Powers and Monoid Instance</a></h2>
<pre><code class="language-lean">def npow (n: ℕ) (x : DCut) : DCut := match n with
  | Nat.zero =&gt; 1
  | Nat.succ k =&gt; x * (npow k x)

theorem npow_zero {x : DCut} : npow 0 x = 1 := by rfl

theorem npow_succ {n : ℕ} {x : DCut} : npow (n+1) x = npow n x * x := by
  simp[npow,mul_comm]

instance monoid_inst : Monoid DCut := ⟨
  @mul_id_left,
  @mul_id_right, -- why does this need to be here if this is already a MulOneClass?
  npow,
  @npow_zero,
  @npow_succ
⟩

instance com_monoid_inst : CommMonoid DCut := ⟨
  @mul_comm
⟩

instance monoid_wz_inst : MonoidWithZero DCut := ⟨
  @mul_zero_left,
  @mul_zero_right
⟩

instance comm_monoid_wz_inst : CommMonoidWithZero DCut := ⟨
  @mul_zero_left,
  @mul_zero_right
⟩


example (x : DCut) : x^2 = x*x := by
  exact pow_two x --&gt; pow_two is a theorem about monoids from Mathlib
</code></pre>
<div style='height=50px'>&nbsp;</div><hr>
Copyright © 2025 Eric Klavins

                    </main>

                    <nav class="nav-wrapper" aria-label="Page navigation">
                        <!-- Mobile navigation buttons -->
                            <a rel="prev" href="../Reals/Max.html" class="mobile-nav-chapters previous" title="Previous chapter" aria-label="Previous chapter" aria-keyshortcuts="Left">
                                <i class="fa fa-angle-left"></i>
                            </a>

                            <a rel="next prefetch" href="../Ordering/Definition.html" class="mobile-nav-chapters next" title="Next chapter" aria-label="Next chapter" aria-keyshortcuts="Right">
                                <i class="fa fa-angle-right"></i>
                            </a>

                        <div style="clear: both"></div>
                    </nav>
                </div>
            </div>

            <nav class="nav-wide-wrapper" aria-label="Page navigation">
                    <a rel="prev" href="../Reals/Max.html" class="nav-chapters previous" title="Previous chapter" aria-label="Previous chapter" aria-keyshortcuts="Left">
                        <i class="fa fa-angle-left"></i>
                    </a>

                    <a rel="next prefetch" href="../Ordering/Definition.html" class="nav-chapters next" title="Next chapter" aria-label="Next chapter" aria-keyshortcuts="Right">
                        <i class="fa fa-angle-right"></i>
                    </a>
            </nav>

        </div>

        <!-- Livereload script (if served using the cli tool) -->
        <script>
            const wsProtocol = location.protocol === 'https:' ? 'wss:' : 'ws:';
            const wsAddress = wsProtocol + "//" + location.host + "/" + "__livereload";
            const socket = new WebSocket(wsAddress);
            socket.onmessage = function (event) {
                if (event.data === "reload") {
                    socket.close();
                    location.reload();
                }
            };

            window.onbeforeunload = function() {
                socket.close();
            }
        </script>



        <script>
            window.playground_copyable = true;
        </script>


        <script src="../elasticlunr.min.js"></script>
        <script src="../mark.min.js"></script>
        <script src="../searcher.js"></script>

        <script src="../clipboard.min.js"></script>
        <script src="../highlight.js"></script>
        <script src="../book.js"></script>

        <!-- Custom JS scripts -->
        <script src="../highlight.js"></script>
        <script src="../lean-book.js"></script>


    </div>
    </body>
</html>
